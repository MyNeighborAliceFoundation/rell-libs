@test module;

import ^^^^^.tests.*;
import ^^^^^^.dip.tests.*;
import ^^^^^^.ft3_helper.tests.*;
import ^^^^^^.originals.marketplace.cache.types.*;

namespace IDecimalTest {
    val interface = "IDecimalTest";
    
    val foo = Attribute.I(interface, name = "foo");
    val color_number = Attribute.I(interface, name = "color_number");

    function register(auth_data) {
        def_interface_op(auth_data.auth_id, interface, [
            foo: Type(Decimal.T(), [Mutable()]),
            color_number: Type(Decimal.T(), [Mutable(), Optional()])
        ])
        .sign(auth_data.keypair)
        .run();
    }

    function get() = Interface.get(interface);
    
    function create_instance(user: auth_data, attributes: map<Attribute.I, attribute_type>) {
        rell.test.tx()
            .op(def_instance_op(
                user.auth_id, 
                IDecimalTest.interface, 
                attributes
            ))
            .nop()
            .sign(user.keypair)
            .run();
        return get_last_instance();
    }
}

function test_attribute_value_can_be_cached() {
    val admin = register_admin();
    
    IDecimalTest.register(admin);
    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.1)
        ]
    );
    
    val values = Cache.Decimal.get_values(Attribute.get(IDecimalTest.foo));

    val expected_values = [
        (
            attribute = Attribute.get_name(IDecimalTest.foo),
            value = 1.1,
            count = 1
        )
    ];

    assert_equals(values, expected_values);
}

function test_multiple_attribute_values_can_be_cached() {
    val admin = register_admin();
    
    IDecimalTest.register(admin);
    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.1)
        ]
    );
    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.2)
        ]
    );
    
    val values = Cache.Decimal.get_values(Attribute.get(IDecimalTest.foo));

    val expected_values = [
        (
            attribute = Attribute.get_name(IDecimalTest.foo),
            value = 1.1,
            count = 1
        ),
        (
            attribute = Attribute.get_name(IDecimalTest.foo),
            value = 1.2,
            count = 1
        )
    ];

    assert_equals(values, expected_values);
}

function test_multiple_attributes_with_same_value_can_be_cached() {
    val admin = register_admin();
    
    IDecimalTest.register(admin);
    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.1)
        ] 
    );

    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.1)
        ]
    );

    val values = Cache.Decimal.get_values(Attribute.get(IDecimalTest.foo));

    val expected_values = [
        (
            attribute = Attribute.get_name(IDecimalTest.foo),
            value = 1.1,
            count = 2
        )
    ];

    assert_equals(values, expected_values);
}

function test_multiple_attributes_with_different_values_can_be_cached_right() {
    val admin = register_admin();
    
    IDecimalTest.register(admin);
    
    val color_instance = IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.1),
            IDecimalTest.color_number: Decimal.T(2.5)
        ]
    );
    
    IDecimalTest.create_instance(
        admin,
        [
            IDecimalTest.foo: Decimal.T(1.2),
            IDecimalTest.color_number: Decimal.T(2.5)
        ]
    );
    
    set_type_attribute_meta_op(
        admin.auth_id, 
        color_instance, 
        IDecimalTest.color_number, 
        (10.5).to_gtv()
    )
    .sign(admin.keypair)
    .run();

    val values = Cache.Decimal.get_values(Attribute.get(IDecimalTest.color_number));

    val expected_values = [
        (
            attribute = Attribute.get_name(IDecimalTest.color_number),
            value = 2.5,
            count = 1
        ),
        (
            attribute = Attribute.get_name(IDecimalTest.color_number),
            value = 10.5,
            count = 1
        )
    ];

    assert_equals(values, expected_values);
}
