namespace Decimal {
	entity decimal_attribute {
		index instance, attribute, value;
		index attribute, value;
		mutable value: decimal;
	}
	
	function T(value: decimal? = null): attribute_type {
		return attribute_type(type = Attribute.type.decimal, value = value?.to_gtv());
	}
	
	function create_value(instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_not_implemented(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value(instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_attribute_can_be_added(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function update_value(instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.update_value_by_attribute(instance, attribute, value);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		var attr = decimal_attribute @? {instance, attribute};
		if(attr == null) {
			add_value(instance, attribute_name, value);
		} else {
			update_value(instance, attribute_name, value);
		}
	}
	
	function remove_value(instance, attribute_name: Attribute.I): decimal {
		val attribute = Attribute.get(attribute_name);
		util.ensure_optional(attribute);
		return Unsafe.remove_value_by_attribute(instance, attribute);
	}	
	
	function get_value (instance, attribute_name: Attribute.I): decimal? { 
		val attribute = Attribute.get(attribute_name);
		var attr = decimal_attribute @? { instance, attribute };
	 	
	 	if(attr??) {
	 		return attr.value;
	 	} else {
	 		val prototypes = Prototype.get_all(instance);
	 		for (p in prototypes) {
	 			attr = decimal_attribute @? { p, attribute };
	 			if(attr??) {
	 				return attr.value;
	 			}
	 		}
	 	}
	 	return null;
	}
	
    function get_value_as_text(instance, attribute_name: Attribute.I): text {
        return "%s".format(get_value(instance, attribute_name));
    }	
	
	function fetch_range_from(attribute, value: decimal, from: rowid, _limit: integer = 100) {
		return decimal_attribute @* {
			attribute, .value == value, .instance.rowid > from
		} (.instance) limit _limit;
	}
	
	function apply_filter(instances: list<instance>, filter: Filter.I_by_attribute, from: Filter.p_from):
	 (results: list<instance>, from: Filter.p_from?) { 	
		val limit_count = 100;
		val attribute = filter.attribute;
		val value = decimal.from_gtv(filter.parameter);
		
		if(instances.empty()) {
			val attribute_name = Attribute.def_I(attribute.dclass.name, attribute.name);
			return Attribute.find_instances(attribute_name, value.to_gtv(), from, limit_count);
		};
		
		var res = list<instance>();
		
		val first = instances[0];
		val last = instances[instances.size() - 1];
		
		val in_prototype = (a: decimal_attribute, ip: instance_prototype) @* {
			a.attribute == attribute, a.value == value,
			ip.prototype == a.instance,
			ip.instance >= first, ip.instance <= last
		} (ip.instance) limit limit_count;
		
		res.add_all(in_prototype);
		
		val in_attribute = (a: decimal_attribute) @* {
				attribute,
				a.value == value,
				a.instance >= first, a.instance <= last
		} (a.instance) limit limit_count;
		
		res.add_all(in_attribute);
		
		val prior_instances = set(instances);
		res = res @* {prior_instances.contains($)};
		
		return (results = res, from = null);
	}
}

namespace Decimal.Unsafe {
	function add_value (instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value_by_attribute(instance, attribute, value: decimal) {
		util.ensure_type(Attribute.type.decimal, attribute.type);
		Hooks.before_add_value(instance, attribute, value);
		val decimal_attribute = create decimal_attribute ( instance, attribute, value );
		Hooks.after_add_value(decimal_attribute);
	}
	
	function update_value (instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		update_value_by_attribute(instance, attribute, value);
	}
	
	function update_value_by_attribute (instance, attribute, value: decimal) {
		util.ensure_type(Attribute.type.decimal, attribute.type);
		val decimal_attribute = decimal_attribute @ { instance, attribute };
		
		Hooks.before_update_value(decimal_attribute, value);
		if (decimal_attribute.value != value) {
		    decimal_attribute.value = value;
		}
		Hooks.after_update_value(decimal_attribute);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: decimal) {
		val attribute = Attribute.get(attribute_name);
		set_value_by_attribute(instance, attribute, value);
	}
	
	function set_value_by_attribute(instance, attribute, value: decimal) {
		var attr = decimal_attribute @? {instance, attribute};
		Hooks.before_set_value(instance, attribute, attr, value);
		if (attr == null) {
			attr = create decimal_attribute ( instance, attribute, value );
		} else {
			attr.value = value;
		}
		Hooks.after_set_value(attr);
	}
	
	function remove_value (instance, attribute_name: Attribute.I): decimal {	
		val attribute = Attribute.get(attribute_name);
		return remove_value_by_attribute(instance, attribute);
	}
	
	function remove_value_by_attribute (instance, attribute): decimal {	
		val decimal_attribute = decimal_attribute @ {instance, attribute};
		val value = decimal_attribute.value;
		
		Hooks.before_remove_value(decimal_attribute);
		delete decimal_attribute;
		Hooks.after_remove_value(instance, attribute, value);
		
		return value;
	}
}

namespace Decimal.Hooks {
    @extendable 
    function before_add_value(instance, attribute, value: decimal);
    
    @extendable 
    function after_add_value(decimal_attribute);
    
    @extendable 
    function before_remove_value(decimal_attribute);
    
    @extendable 
    function after_remove_value(instance, attribute, value: decimal);
    
    @extendable 
    function before_update_value(decimal_attribute, value: decimal);
    
    @extendable 
    function after_update_value(decimal_attribute);
    
    @extendable 
    function before_set_value(instance, attribute, decimal_attribute?, value: decimal);
    
    @extendable 
    function after_set_value(decimal_attribute);
}

namespace Decimal.Private {

    function is_unique_value(instance, attribute, value: decimal) {
        val decimal_attribute = decimal_attribute @? { attribute, value } limit 1;
        return decimal_attribute == null or decimal_attribute.instance == instance;
    }    
}
