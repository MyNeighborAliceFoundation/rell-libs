namespace File {
	entity file_attribute {
		index instance, attribute, hash;
		index instance, attribute;
		index attribute, hash;
		index hash, hub_brid, hub_location;
		mutable hash: byte_array;
		mutable hub_brid: byte_array;
		mutable hub_location: text;
	}
	
	function T(value: File.data? = null): attribute_type {
		return attribute_type(type = Attribute.type.file, value = value?.to_gtv());
	}

	struct data {
	    hash: byte_array;
	    hub_brid: byte_array;
	    hub_location: text;
	}
	
	function create_value(instance, attribute_name: Attribute.I, value: data) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_not_implemented(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value(instance, attribute_name: Attribute.I, value: data) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_attribute_can_be_added(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function update_value(instance, attribute_name: Attribute.I, value: data) {
		val attribute = Attribute.get(attribute_name);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.update_value_by_attribute(instance, attribute, value);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: data) {
		val attribute = Attribute.get(attribute_name);
		var attr = file_attribute @? {instance, attribute};
		if(attr == null) {
			add_value(instance, attribute_name, value);
		} else {
			update_value(instance, attribute_name, value);
		}
	}
	
	function remove_value(instance, attribute_name: Attribute.I): data {
		val attribute = Attribute.get(attribute_name);
		util.ensure_optional(attribute);
		return Unsafe.remove_value_by_attribute(instance, attribute);
	}
	
	function get_value (instance, attribute_name: Attribute.I): data? {
		val attribute = Attribute.get(attribute_name);
		var attr = file_attribute @? { instance, attribute };
	 	
	 	if(attr??) {
	 		return data(
	 			hash = attr.hash,
	 			hub_brid = attr.hub_brid, 
	 			hub_location = attr.hub_location
	 		);
	 	} else {
	 		val prototypes = Prototype.get_all(instance);
	 		for (p in prototypes) {
	 			attr = file_attribute @? { p, attribute };
	 			if(attr??) {
	 				return data(
			 			hash = attr.hash,
			 			hub_brid = attr.hub_brid, 
			 			hub_location = attr.hub_location
			 		);
	 			}
	 		}
	 	}
	 	
	 	return null;
	}
	
	function get_value_as_text(instance, attribute_name: Attribute.I): text {
	    require(false, "file get_value_as_text: Not implementation");
        return "";
    }
	
	function fetch_range_from(attribute, file_hash: byte_array, from: rowid, _limit: integer = 100) {
		return file_attribute @* {
			attribute, .hash == file_hash, .instance.rowid > from
		} (.instance) limit 100;
	}
	
	function apply_filter(instances: list<instance>, filter: Filter.I_by_attribute, from: Filter.p_from):
	 (results: list<instance>, from: Filter.p_from?) { 	
		val limit_count = 100;
		val attribute = filter.attribute;
		val value = byte_array.from_gtv(filter.parameter);
		
		if(instances.empty()) {
			val attribute_name = Attribute.def_I(attribute.dclass.name, attribute.name);
			return Attribute.find_instances(attribute_name, value.to_gtv(), from, limit_count);
		};
		
		var res = list<instance>();
		
		val first = instances[0];
		val last = instances[instances.size() - 1];
		
		val in_prototype = (a: file_attribute, ip: instance_prototype) @* {
			a.attribute == attribute, a.hash == value,
			ip.prototype == a.instance,
			ip.instance >= first, ip.instance <= last
		} (ip.instance) limit limit_count;
		
		res.add_all(in_prototype);
		
		val in_attribute = (a: file_attribute) @* {
				attribute,
				a.hash == value,
				a.instance >= first, a.instance <= last
		} (a.instance) limit limit_count;
		
		res.add_all(in_attribute);
		
		val prior_instances = set(instances);
		res = res @* {prior_instances.contains($)};
		
		return (results = res, from = null);
	}
}


namespace File.Unsafe {
	function add_value (instance, attribute_name: Attribute.I, data) {
		val attribute = Attribute.get(attribute_name);
		add_value_by_attribute(instance, attribute, data);
	}
	
	function add_value_by_attribute(instance, attribute, data) {
		util.ensure_type(Attribute.type.file, attribute.type);
		Hooks.before_add_value(instance, attribute, data);
		val file_attribute = create file_attribute (
			instance,
			attribute,
			.hash = data.hash,
			.hub_brid = data.hub_brid,
			.hub_location = data.hub_location
		);
		Hooks.after_add_value(file_attribute);
	}
	
	function update_value(instance, attribute_name: Attribute.I, data) {
		val attribute = Attribute.get(attribute_name);
		update_value_by_attribute(instance, attribute, data);
	}
	
	function update_value_by_attribute(instance, attribute, data) {
		util.ensure_type(Attribute.type.file, attribute.type);
		update file_attribute @ { instance, attribute } (
			.hash = data.hash,
			.hub_brid = data.hub_brid,
			.hub_location = data.hub_location
		);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: data) {
		val attribute = Attribute.get(attribute_name);
		set_value_by_attribute(instance, attribute, value);
	}
	
	function set_value_by_attribute(instance, attribute, data) {
		var attr = file_attribute @? {instance, attribute};
		Hooks.before_set_value(instance, attribute, attr, data);
		if(attr == null) {
			attr = create file_attribute (
				instance,
				attribute,
				.hash = data.hash,
				.hub_brid = data.hub_brid,
				.hub_location = data.hub_location
			);
		} else {
			attr.hash = data.hash;
			attr.hub_brid = data.hub_brid;
			attr.hub_location = data.hub_location;
		}
		Hooks.after_set_value(attr);
	}
	
	function remove_value (instance, attribute_name: Attribute.I): data {	
		val attribute = Attribute.get(attribute_name);
		return remove_value_by_attribute(instance, attribute);
	}
	
	function remove_value_by_attribute (instance, attribute): data {	
		val file_attribute = file_attribute @ {instance, attribute};
		val value = data(
			hash = file_attribute.hash,
			hub_brid = file_attribute.hub_brid, 
			hub_location = file_attribute.hub_location
		);
		
		Hooks.before_remove_value(file_attribute);
		delete file_attribute;
		Hooks.after_remove_value(instance, attribute, value);
		
		return value;
	}
}

namespace File.Hooks {
    @extendable 
    function before_add_value(instance, attribute, value: data);
    
    @extendable 
    function after_add_value(file_attribute);
    
    @extendable 
    function before_remove_value(file_attribute);
    
    @extendable 
    function after_remove_value(instance, attribute, value: data);
    
    @extendable 
    function before_update_value(file_attribute, value: data);
    
    @extendable 
    function after_update_value(file_attribute);
    
    @extendable 
    function before_set_value(instance, attribute, file_attribute?, value: data);
    
    @extendable 
    function after_set_value(file_attribute);
}

namespace File.Private {
        
    function is_unique_value(instance, attribute, value: data) {
        // Does having the same hash makes file types equal or we should also check file hub info?
        val file_attribute = file_attribute @? { attribute, .hash == value.hash } limit 1;
        return file_attribute == null or file_attribute.instance == instance;
    }
}
