namespace Instance {
	entity instance_attribute {
		index instance, attribute, value;
		index attribute, value;
		mutable value: instance;
	}
	
	function T(value: instance? = null): attribute_type {
		return attribute_type(type = Attribute.type.instance, value = value?.to_gtv());
	}
	
	function create_value(instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		require(not value.is_prototype, "Prototypes cannot be linked.");
		util.ensure_not_implemented(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value(instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		require(not value.is_prototype, "Prototypes cannot be linked.");
		util.ensure_attribute_can_be_added(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function update_value(instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		require(not value.is_prototype, "Prototypes cannot be linked.");
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.update_value_by_attribute(instance, attribute, value);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		var attr = instance_attribute @? {instance, attribute};
		if(attr == null) {
			add_value(instance, attribute_name, value);
		} else {
			update_value(instance, attribute_name, value);
		}
	}
	
	function remove_value(instance, attribute_name: Attribute.I): instance {
		val attribute = Attribute.get(attribute_name);
		util.ensure_optional(attribute);
		return Unsafe.remove_value_by_attribute(instance, attribute);
	}	
	
	function get_value (instance, attribute_name: Attribute.I): instance? { 
		val attribute = Attribute.get(attribute_name);
		var attr = instance_attribute @? { instance, attribute };
	 	
	 	if(attr??) {
	 		return attr.value;
	 	} else {
	 		val prototypes = Prototype.get_all(instance);
	 		for (p in prototypes) {
	 			attr = instance_attribute @? { .instance == p, attribute };
	 			if(attr??) {
	 				return attr.value;
	 			}
	 		}
	 	}
	 	return null;
	}
	
    function get_value_as_text(instance, attribute_name: Attribute.I): text {
        require(false, "instance get_value_as_text: Not implementation");
        return "";
    }	
	
	function fetch_range_from(attribute, value: instance, from: rowid, _limit: integer = 100) {
		return instance_attribute @* {
			attribute, .value == value, .instance.rowid > from
		} (.instance) limit _limit;
	}
	
	function apply_filter(instances: list<instance>, filter: Filter.I_by_attribute, from: Filter.p_from):
	 (results: list<instance>, from: Filter.p_from?) { 	
		val limit_count = 100;
		val attribute = filter.attribute;
		val value = instance.from_gtv(filter.parameter);
		
		if(instances.empty()) {
			val attribute_name = Attribute.def_I(attribute.dclass.name, attribute.name);
			return Attribute.find_instances(attribute_name, value.to_gtv(), from, limit_count);
		};
		
		var res = list<instance>();
		
		val first = instances[0];
		val last = instances[instances.size() - 1];
		
		val in_prototype = (a: instance_attribute, ip: instance_prototype) @* {
			a.attribute == attribute, a.value == value,
			ip.prototype == a.instance,
			ip.instance >= first, ip.instance <= last
		} (ip.instance) limit limit_count;
		
		res.add_all(in_prototype);
		
		val in_attribute = (a: instance_attribute) @* {
				attribute,
				a.value == value,
				a.instance >= first, a.instance <= last
		} (a.instance) limit limit_count;
		
		res.add_all(in_attribute);
		
		val prior_instances = set(instances);
		res = res @* {prior_instances.contains($)};
		
		return (results = res, from = null);
	}
}

namespace Instance.Unsafe {
	function add_value (instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value_by_attribute(instance, attribute, value: instance) {
		util.ensure_type(Attribute.type.instance, attribute.type);
		Hooks.before_add_value(instance, attribute, value);
		val instance_attribute = create instance_attribute ( instance, attribute, value );
		Hooks.after_add_value(instance_attribute);
	}
	
	function update_value (instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		update_value_by_attribute(instance, attribute, value);
	}
	
	function update_value_by_attribute (instance, attribute, value: instance) {
		util.ensure_type(Attribute.type.instance, attribute.type);
		val instance_attribute = instance_attribute @ { instance, attribute };
		
        Hooks.before_update_value(instance_attribute, value);
        if (instance_attribute.value != value) {
            instance_attribute.value = value;
        }
        Hooks.after_update_value(instance_attribute);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: instance) {
		val attribute = Attribute.get(attribute_name);
		set_value_by_attribute(instance, attribute, value);
	}
	
	function set_value_by_attribute(instance, attribute, value: instance) {
		var attr = instance_attribute @? {instance, attribute};
		Hooks.before_set_value(instance, attribute, attr, value);
		if (attr == null) {
			attr = create instance_attribute ( instance, attribute, value );
		} else {
			attr.value = value;
		}
		Hooks.after_set_value(attr);
	}
	
	function remove_value (instance, attribute_name: Attribute.I): instance {	
		val attribute = Attribute.get(attribute_name);
		return remove_value_by_attribute(instance, attribute);
	}
	
	function remove_value_by_attribute (instance, attribute): instance {	
		val instance_attribute = instance_attribute @ {instance, attribute};
		val value = instance_attribute.value;
		
		Hooks.before_remove_value(instance_attribute);
		delete instance_attribute;
		Hooks.after_remove_value(instance, attribute, value);
		
		return value;
	}
}


namespace Instance.Hooks {
    @extendable 
    function before_add_value(instance, attribute, value: instance);
    
    @extendable 
    function after_add_value(instance_attribute);
    
    @extendable 
    function before_remove_value(instance_attribute);
    
    @extendable 
    function after_remove_value(instance, attribute, value: instance);
    
    @extendable 
    function before_update_value(instance_attribute, value: instance);
    
    @extendable 
    function after_update_value(instance_attribute);
    
    @extendable 
    function before_set_value(instance, attribute, instance_attribute?, value: instance);
    
    @extendable 
    function after_set_value(instance_attribute);
}

namespace Instance.Private {
    function is_unique_value(instance, attribute, value: instance) {
        // How to compare instances?
        require(false, "instance is_unique_value: Not implemented");
        return true;
    }
}

