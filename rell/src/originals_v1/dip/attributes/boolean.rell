namespace Boolean {
	entity boolean_attribute {
		index instance, attribute, value;
		index attribute, value;
		mutable value: boolean;
	}
	
	function T(value: boolean? = null): attribute_type {
		return attribute_type(type = Attribute.type.boolean, value = value?.to_gtv());
	}
	
	function create_value(instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_not_implemented(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value(instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		util.ensure_attribute_can_be_added(instance, attribute);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.add_value_by_attribute(instance, attribute, value);
	}
	
	function update_value(instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		Meta.Engine.execute_value(instance, attribute, value.to_gtv());
		Unsafe.update_value_by_attribute(instance, attribute, value);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		var attr = boolean_attribute @? {instance, attribute};
		if(attr == null) {
			add_value(instance, attribute_name, value);
		} else {
			update_value(instance, attribute_name, value);
		}
	}
	
	function remove_value(instance, attribute_name: Attribute.I): boolean {
		val attribute = Attribute.get(attribute_name);
		util.ensure_optional(attribute);
		return Unsafe.remove_value_by_attribute(instance, attribute);
	}	
	
	function get_value (instance, attribute_name: Attribute.I): boolean? { 
		val attribute = Attribute.get(attribute_name);
		var attr = boolean_attribute @? { instance, attribute };
	 	
	 	if(attr??) {
	 		return attr.value;
	 	} else {
	 		val prototypes = Prototype.get_all(instance);
	 		for (p in prototypes) {
	 			attr = boolean_attribute @? { p, attribute };
	 			if(attr??) {
	 				return attr.value;
	 			}
	 		}
	 	}
	 	return null;
	}
	
	function get_value_as_text(instance, attribute_name: Attribute.I): text {
	    return "%s".format(get_value(instance, attribute_name));
	}
	
	function fetch_range_from(attribute, value: boolean, from: rowid, _limit: integer = 100) {
		return boolean_attribute @* {
			attribute, .value == value, .instance.rowid > from
		} (.instance) limit _limit;
	}
	
	function apply_filter(instances: list<instance>, filter: Filter.I_by_attribute, from: Filter.p_from):
	 (results: list<instance>, from: Filter.p_from?) { 	
		val limit_count = 100;
		val attribute = filter.attribute;
		val value = boolean.from_gtv(filter.parameter);
		
		if(instances.empty()) {
			val attribute_name = Attribute.def_I(attribute.dclass.name, attribute.name);
			return Attribute.find_instances(attribute_name, value.to_gtv(), from, limit_count);
		};
		
		var res = list<instance>();
		
		val first = instances[0];
		val last = instances[instances.size() - 1];
		
		val in_prototype = (a: boolean_attribute, ip: instance_prototype) @* {
			a.attribute == attribute, a.value == value,
			ip.prototype == a.instance,
			ip.instance >= first, ip.instance <= last
		} (ip.instance) limit limit_count;
		
		res.add_all(in_prototype);
		
		val in_attribute = (a: boolean_attribute) @* {
				attribute,
				a.value == value,
				a.instance >= first, a.instance <= last
		} (a.instance) limit limit_count;
		
		res.add_all(in_attribute);
		
		val prior_instances = set(instances);
		res = res @* {prior_instances.contains($)};
		
		return (results = res, from = null);
	}
}

namespace Boolean.Unsafe {
	function add_value (instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		add_value_by_attribute(instance, attribute, value);
	}
	
	function add_value_by_attribute(instance, attribute, value: boolean) {
		util.ensure_type(Attribute.type.boolean, attribute.type);
		Hooks.before_add_value(instance, attribute, value);
		val boolean_attribute = create boolean_attribute ( instance, attribute, value );
		Hooks.after_add_value(boolean_attribute);
	}
	
	function update_value (instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		update_value_by_attribute(instance, attribute, value);
	}
	
	function update_value_by_attribute (instance, attribute, value: boolean) {
		util.ensure_type(Attribute.type.boolean, attribute.type);
		val boolean_attribute = boolean_attribute @ { instance, attribute };
		
		Hooks.before_update_value(boolean_attribute, value);
		if (boolean_attribute.value != value) {
		    boolean_attribute.value = value;
		}
		Hooks.after_update_value(boolean_attribute);
	}
	
	function set_value(instance, attribute_name: Attribute.I, value: boolean) {
		val attribute = Attribute.get(attribute_name);
		set_value_by_attribute(instance, attribute, value);
	}
	
	function set_value_by_attribute(instance, attribute, value: boolean) {
		var attr = boolean_attribute @? {instance, attribute};
		
		Hooks.before_set_value(instance, attribute, attr, value);
		if(attr == null) {
			attr = create boolean_attribute ( instance, attribute, value );
		} else {
			attr.value = value;
		}
		Hooks.after_set_value(attr);
	}
	
	function remove_value (instance, attribute_name: Attribute.I): boolean {	
		val attribute = Attribute.get(attribute_name);
		return remove_value_by_attribute(instance, attribute);
	}
	
	function remove_value_by_attribute (instance, attribute): boolean {	
		val boolean_attribute = boolean_attribute @ {instance, attribute};
		val value = boolean_attribute.value;
		
		Hooks.before_remove_value(boolean_attribute);
		delete boolean_attribute;
		Hooks.after_remove_value(instance, attribute, value);
		
		return value;
	}
}

namespace Boolean.Hooks {
    @extendable 
    function before_add_value(instance, attribute, value: boolean);
    
    @extendable 
    function after_add_value(boolean_attribute);
    
    @extendable 
    function before_remove_value(boolean_attribute);
    
    @extendable 
    function after_remove_value(instance, attribute, value: boolean);
    
    @extendable 
    function before_update_value(boolean_attribute, value: boolean);
    
    @extendable 
    function after_update_value(boolean_attribute);
    
    @extendable 
    function before_set_value(instance, attribute, boolean_attribute?, value: boolean);
    
    @extendable 
    function after_set_value(boolean_attribute);
}

namespace Boolean.Private {
    function is_unique_value(instance, attribute, value: boolean) {
        val boolean_attribute = boolean_attribute @? { attribute, value } limit 1;
        return boolean_attribute == null or boolean_attribute.instance == instance;
    }
}

